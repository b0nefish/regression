		device	zxspectrum128

@DEBUG = 0

		include "../kernel/constants.inc"

;;-----------------------------------------------------------------------------
	macro border n
	  if DEBUG
	    if n > 0
		ld	a,n
	    else
		xor	a
	    endif
		out	(254),a
	  endif
	endm

;;-----------------------------------------------------------------------------

		org	RUNZONE
		define	MUSIC.waitfor 1321

@bufdat = #BEBA

begin:
	ifndef fx
		ld	hl,initscreen
		ld	de,#4000
		call	depack
	endif

		ld	hl,bufdat
		ld	b,75
		call	slucka
		ld	hl,bufdat
		ld	b,75
		call	slucka
		ld	hl,bufdat
		ld	b,23+26
		call	slucka
		call	waitf	; urob lasku!
		ld	b,26
		call	slucka
		ld	hl,bufdat
		ld	b,75
		call	slucka
		ld	hl,bufdat
		ld	b,75
		call	slucka
		ld	hl,bufdat
		ld	b,23
		call	slucka
		call	waitf	; urob lasku!
		ld	b,26+26
		call	slucka
		ld	hl,bufdat
		ld	b,75
		call	slucka
		ld	hl,bufdat
		ld	b,75
		call	slucka
		call	waitf	; urob lasku!

.loop		ei
		halt
		jr	.loop

slucka:		ei
		halt
		ld	a,(hl)
		dec	a
		ret	z
		push	bc
		border	1
		call	play
		border	0
		pop	bc
		inc	hl
		djnz	slucka
		ret

waitf:		ld	b,44
.loop		ei
		halt
		djnz	.loop
		ret

play0:		call	drawchunk
play:		ld	a,(hl)
		and	#1F
		sub	#08
		inc	a
		jr	z,linearchunk
		ret	m
		add	a,#3F
		ld	d,a
		ld	a,(hl)
		inc	hl
		ld	e,(hl)
		inc	hl
		and	#E0
		jr	nz,play0
		ld	a,(hl)
		and	#1F
		ld	b,a
		ld	a,(hl)
		inc	hl
		ld	c,(hl)
		inc	hl
		push	hl
		sbc	hl,bc
		call	drawchunk
		pop	hl
		jr	play

drawchunk:	rrca
		rrca
		rrca
		cpl
		and	#1C
		ld	c,a
		rrca
		rrca
		add	a,c
		ld	($+4),a
		jr	$
	rept	8
		ld	a,(de)
		xor	(hl)
		ld	(de),a
		inc	hl
		inc	d
	endm
		org	$-1
		ret

linearchunk:	ld	a,(hl)
		and	#E0
		rrca
		add	a,#10
		rrca
		rrca
		rrca
		ld	b,a
		inc	hl
		ld	e,(hl)
		inc	hl
		ld	a,#40
		add	a,(hl)
		ld	d,a
.loop:		inc	hl
		ld	a,(de)
		xor	(hl)
		ld	(de),a
		inc	e
		djnz	.loop
		inc	hl
		jr	play

	ifndef fx
initscreen:	incbin "gfx/oneplusani000.scr.lzx"
	else
		savebin "final.bin",begin,$-begin
	endif

;;-----------------------------------------------------------------------------
	ifndef fx
		slot 3
		page 6

		include "../kernel/kernel.micro.inc"

		slot 3
		page 0

		org	bufdat
		incbin	"oneplusani.ani.bin"

		savesna "oneplus.sna",microkernel
	endif
